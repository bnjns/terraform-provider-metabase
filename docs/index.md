---
page_title: "Provider: Metabase"
description: |-
    This provider can be used to manage an instance of Metabase.
---

# Metabase Provider

The Metabase provider allows you to configure a [Metabase](https://www.metabase.com/) instance using
infrastructure-as-code.

## Example Usage

```terraform
terraform {
  required_providers {
    metabase = {
      source  = "bnjns/metabase"
      version = "~> 0.0"
    }
  }
}

provider "metabase" {
  host = "<your metabase host/ip>"

  # Optionally configure headers to set on each request
  headers = {
    Authorizer = "Bearer: ${var.id_token}"
  }
}
```

## Authentication

You must configure the Metabase provider with the username (email) and password of a super user. In general, it is
recommended that the provider uses a "machine user", which is a user account that no actual users use and exists purely
for the provider.

!> **Warning:** Hard-coded credentials are not recommended in any Terraform configuration and risks secret leakage.

### Using env variables

The simplest way to configure the provider is to configure the host, username, and password using the environment variables:

- `METABASE_HOST`
- `METABASE_USERNAME`
- `METABASE_PASSWORD`

```terraform
# The provider will be automatically configured with the METABASE_HOST, METABASE_USERNAME and METABASE_PASSWORD
# environment variables.
provider "metabase" {}
```

### Using another provider

You can also use another provider to set the credentials for you. For example, using AWS and Secrets Manager:

```terraform
provider "aws" {
  # Your AWS provider config goes here
}
data "aws_secretsmanager_secret" "metabase_credentials" {
  name = "metabase-credentials"
}
data "aws_secretsmanager_secret_version" "metabase_credentials" {
  secret_id = data.aws_secretsmanager_secret.metabase_credentials.id
}
locals {
  metabase_credentials = jsondecode(data.aws_secretsmanager_secret_version.metabase_credentials.secret_string)
}

provider "metabase" {
  host     = local.metabase_credentials["host"]
  username = local.metabase_credentials["username"]
  password = local.metabase_credentials["password"]
}
```

> This example assumes a JSON secret, but it can be any structure.

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `headers` (Map of String, Sensitive) Optional headers to attach to every request to Metabase.
- `host` (String) The Host URL of the Metabase instance to manage. Can also be set with the METABASE_HOST environment variable.
- `password` (String, Sensitive) The password of the super user to use when interacting with Metabase. Can also be set with the METABASE_PASSWORD environment variable.
- `username` (String) The username of the super user to use when interacting with Metabase. Can also be set with the METABASE_USERNAME environment variable.

