---
page_title: "Provider: Metabase"
description: |-
    This provider can be used to manage an instance of Metabase.
---

# Metabase Provider

The Metabase provider allows you to configure a [Metabase](https://www.metabase.com/) instance using
infrastructure-as-code.

## Example Usage

```terraform
terraform {
  required_providers {
    metabase = {
      source  = "bnjns/metabase"
      version = "~> 0.0"
    }
  }
}

provider "metabase" {
  host = "<your metabase host/ip>"

  # Optionally configure headers to set on each request
  headers = {
    Authorizer = "Bearer: ${var.id_token}"
  }
}
```

## Authentication

Metabase supports 2 authentication methods, which are used in the following order:

1. [An API key](https://www.metabase.com/docs/latest/people-and-groups/api-keys) (v0.49 or later)
2. Username (email) and password

-> The API key or user should be a member of the _Administrators_ group so that it has access to the entire API.

## Configuring the provider

Most properties can be configured either using the provider attributes or environment variables:

| Setting              | Provider Attribute | Environment variable |
|----------------------|--------------------|----------------------|
| Metabase Host URL/IP | `host`             | `METABASE_HOST`      |
| API Key              | `api_key`          | `METABASE_API_KEY`   |
| Username (email)     | `username`         | `METABASE_USERNAME`  |
| Password             | `password`         | `METABASE_PASSWORD`  |

### Explicit provider attributes

```terraform
provider "metabase" {
  host    = "<your host>"
  api_key = "<your api key>"
}

provider "metabase" {
  host     = "<your host>"
  username = "<your username>"
  password = "<your password>"
}
```

!> **Warning:** Hard-coded credentials are not recommended in any Terraform configuration and risks secret leakage.

### Using env variables

```terraform
# The provider will be automatically configured with the METABASE_HOST,
# and METABASE_API_KEY or METABASE_USERNAME and METABASE_PASSWORD
# environment variables.
provider "metabase" {}
```

### Using another provider

You can also use another provider to set the credentials for you. For example, using AWS and Secrets Manager:

```terraform
provider "aws" {
  # Your AWS provider config goes here
}
data "aws_secretsmanager_secret" "metabase_credentials" {
  name = "metabase-credentials"
}
data "aws_secretsmanager_secret_version" "metabase_credentials" {
  secret_id = data.aws_secretsmanager_secret.metabase_credentials.id
}
locals {
  metabase_credentials = jsondecode(data.aws_secretsmanager_secret_version.metabase_credentials.secret_string)
}

provider "metabase" {
  host     = local.metabase_credentials["host"]
  username = local.metabase_credentials["username"]
  password = local.metabase_credentials["password"]
}
```

> This example assumes a JSON secret, but it can be any structure.

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `api_key` (String, Sensitive) The API key to use for authenticating with Metabase. Can also be set with the METABASE_API_KEY environment variable.
- `headers` (Map of String, Sensitive) Optional headers to attach to every request to Metabase.
- `host` (String) The Host URL of the Metabase instance to manage. Can also be set with the METABASE_HOST environment variable.
- `password` (String, Sensitive) The password of the super user to use when interacting with Metabase. Can also be set with the METABASE_PASSWORD environment variable.
- `username` (String) The username of the super user to use when interacting with Metabase. Can also be set with the METABASE_USERNAME environment variable.


